## coffee -cs

notify = window.webkitNotifications
return unless notify?

$popupQueue = []
$updateCount = 0

registerPlugin
  update: ->
    $updateCount++
  miscTab: (ele) ->
    div = document.createElement 'div'
    form = document.createElement 'form'
    form.onsubmit = (e) ->
      notify.requestPermission ->
        status = document.getElementById('notification_status')
        status.textContent = 'on'
        timeout = document.getElementById('notification_timeout')
        writeCookie 'notification_timeout', timeout.value, 3652
      return false
    div.appendChild form
    state = document.createElement 'span'
    state.id = 'notification_status'
    state.textContent = if notify.checkPermission() is 0 then 'on' else 'off'
    request = document.createElement 'input'
    request.type = 'submit'
    request.value = 'Request'
    form.appendChild document.createTextNode 'Notification state: '
    form.appendChild state
    form.appendChild document.createTextNode ' '
    form.appendChild request
    timeout = document.createElement 'input'
    timeout.type = 'text'
    timeout.id = 'notification_timeout'
    timeout.size = 3
    timeout.value = readCookie('notification_timeout') or 5000
    form.appendChild document.createElement 'br'
    form.appendChild document.createTextNode 'Notification timeout: '
    form.appendChild timeout
    form.appendChild document.createTextNode ' msec'
    hr = document.createElement 'hr'
    hr.className = 'spacer'
    ele.appendChild div
    ele.appendChild hr
  gotNewMessage: (tw) ->
    return if $updateCount < 2 or notify.checkPermission() isnt 0 or tw.user.screen_name is myname
    popup = notify.createNotification tw.user.profile_image_url, tw.user.name, tw.text
    popup.ondisplay = (e) ->
      timeout = readCookie('notification_timeout') or 5000
      if timeout > 0
        setTimeout ->
          e.currentTarget.cancel()
        , timeout
    popup.onclose = (e) ->
      $popupQueue.shift()
      $popupQueue[0].show() if $popupQueue.length > 0
    $popupQueue.push popup
    popup.show() if $popupQueue.length is 1




# __END__
# vim: filetype=coffee
